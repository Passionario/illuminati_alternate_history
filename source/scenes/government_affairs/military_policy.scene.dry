title: Military Policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: military_policy_timer += 6; month_actions += 1
view-if: spd_in_government = 1 and reichswehr_minister_party = "SPD" and military_policy_timer <= 0 and disband_military = 0
card-image: img/portraits/LeberJulius.jpg

= Military Policy

The Reichswehr is a deeply reactionary institution that has been generally hostile to any notion of socialism or democracy. However, we now control the military as part of the government. What should we do to reform it?

- @increase_funding: Increase funding for the military to purchase its loyalty.
- @decrease_funding: Reduce funding for the military so we have more for social spending.
- @reform: Conduct a thorough reform of the ranks, reducing the influence of reactionaries and making it more hospitable to us.
- @disband_reichswehr
- @remilitarize
- @remilitarize_two
- @root: No change to military policy.
- @easy_discard

@increase_funding
subtitle: -1 budget
choose-if: budget >= 1
unavailable-subtitle: We do not have the budget.
on-arrival: budget -= 1; coalition_dissent -= 1 if coalition_dissent > 0; reichswehr_loyalty += 0.1; reichswehr_militancy += 0.5; reichswehr_strength += 20; kpd_coalition_dissent += 1; west_relation -= 1; east_relation -= 1; reparations_negotiation -= 1

The military has more guns and more soldiers. The military and the center-right parties are pleasantly surprised that we would increase military funding. Our membership tends to be anti-military, and is less enthused. Germany's neighbors are watching our military buildup with worry. [? if reparations >= 0 : This is not good for the reparations negotiations. ?]

@decrease_funding
view-if: military_rearmed == 0
subtitle: +1 budget
unavailable-subtitle: There is nothing left to cut.
choose-if: reichswehr_strength > 0 
on-arrival: budget += 1; coalition_dissent += 1 if in_grand_coalition == 1; dvp_relation -= 8; reichswehr_militancy -= 1; reichswehr_strength -= 20; sh_strength += 15; sa_strength += 5; sh_militancy += 0.05; reichswehr_loyalty -= 0.05 if reichswehr_loyalty > 0; coup_progress += 1; coup_progress += 1 if reichswehr_loyalty < 0.2; coup_progress += 1 if reichswehr_loyalty < 0.1; coup_progress += 1 if reichswehr_loyalty <= 0; reichswehr_goal_completed = 1;

The military has fewer guns and fewer soldiers. There is no hint of a coup yet, but we do not know where the line will be drawn. The center-right parties are committed to the military and deeply opposed to our budget cuts.

Many demobilized soldiers have joined the right-wing paramilitaries such as the Stahlhelm and SA, and weapons are missing from our stockpiles.

@disband_reichswehr
title: Declare total demilitarization and disband the military.
view-if: disband_military == 0 and reichswehr_strength <= 0 and military_rearmed == 0
unavailable-subtitle: [? if nationalism >= 42 : Nationalism is too strong. ?] [? if pacifism <= 5 : There is insufficient support for pacifism. ?] [? if judicial_reform < 4 : The judiciary would never allow this. ?]
choose-if: pacifism > 5 and nationalism < 42 and judicial_reform > 3
on-arrival: disband_military = 1; coalition_dissent += 2 if (in_grand_coalition or in_weimar_coalition); coalition_dissent += 2 if (reparations == -2 and (in_grand_coalition or in_weimar_coalition)); reichswehr_militancy = 0; reichswehr_loyalty = 0; left_dissent -= 15; center_dissent += 10; coup_progress += 2; coup_progress += 1 if reparations == -2; reichswehr_goal_completed = 1; reichswehr_minister = "N/A"; reichswehr_minister_party = "N/A"; kpd_relation += 8; workers_kpd += 3; unemployed_kpd += 3; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); new_middle_spd -= 5 if reparations == -2; old_middle_spd -= 5 if reparations == -2; z_relation -= 3; dvp_relation -= 3; z_relation -= 5 if reparations == -2; dvp_relation -= 3 if reparations == -2; communist_coalition += 1; rfb_strength += 100; rfb_militancy += 0.1; sa_strength += 50; sh_strength += 50; west_relation += 3; east_relation += 3; reparations_negotiation += 3; workers_spd += 8*(1-dissent) if reparations > -2; old_middle_spd += 5*(1-dissent) if reparations > -2; new_middle_spd += 5*(1-dissent) if reparations > -2; rural_spd += 5*(1-dissent)  if reparations > -2; reparations = -2

Our attempt is successful and Reichswehr is no more! The centrists and reactionaries are fuming with terror and anger, decrying us as national traitors. Our European neighbours praise our decision as the crowning moment of Pax Versailles[? if reparations > -2 : , and pause our remaining reparations payments, which blunts some of the criticism ?]. 

@remilitarize
title: Commit to rearmament of Reichswehr.
subtitle: -1 budget. There may be consequences.
view-if: military_rearmed == 0 and reichswehr_strength >= 160 
choose-if: budget >= 1 and judiciary <= 2
unavailable-subtitle: [? if budget <= 0 : We do not have the budget. ?] [? if (judicial_reform > 3 and corrupt_judiciary == 0) : The judiciary would never allow this violation of our international treaties. ?][? if (judicial_reform > 3 and corrupt_judiciary >= 1) : The judiciary would never allow this violation of our international treaties, even with our pet jurists tilting the scales. ?]
on-arrival: budget -= 1; military_rearmed += 1; coalition_dissent -= 1 if coalition_dissent > 0; reichswehr_loyalty += 0.2; reichswehr_strength += 100; reichswehr_militancy += 2; kpd_coalition_dissent += 2; west_relation -= 2; east_relation -= 2; reparations_negotiation -= 3; banking_crisis_timer = 2;
max-visits: 1

The military and the center-right parties are delighted by our commitment to military rearmament. Our membership tends to be anti-military, and is very alarmed. Germany's neighbors declare our actions a violation of the Treaty of Versailles and promise dire repercussions.

@remilitarize_two
title: Continue the rearmament of Reichswehr.
subtitle: -1 budget. This will not end well.
view-if: military_rearmed >= 1 
choose-if: budget >= 1 and judiciary <= 2
unavailable-subtitle: [? if budget <= 0 : We do not have the budget. ?] [? if (judicial_reform > 3 and corrupt_judiciary == 0) : The judiciary would never allow this violation of our international treaties. ?][? if (judicial_reform > 3 and corrupt_judiciary >= 1) : The judiciary would never allow this violation of our international treaties, even with our pet jurists tilting the scales. ?]
on-arrival: budget -= 1; military_rearmed += 1; world_war_two += 3; coalition_dissent -= 1 if coalition_dissent > 0; reichswehr_loyalty += 0.2; reichswehr_strength += 300; reichswehr_militancy += 2; kpd_coalition_dissent += 2; west_relation -= 4; east_relation -= 4; reparations_negotiation -= 4
max-visits: 2

The military has more soldiers and a lot more guns. Germany's neighbors respond by ramping up their own military buildup. 

@reform
subtitle: -1 budget
choose-if: budget >= 1
unavailable-subtitle: We do not have the budget.
on-arrival: coalition_dissent += 1 if in_grand_coalition == 1; reichswehr_loyalty += 0.15; reichswehr_militancy -= 1; budget -= 1; coup_progress += 1

Our efforts are at least somewhat successful; we have changed the recruiting and training patterns so that more soldiers from non-reactionary backgrounds can join, hopefully changing the culture of the forces. We have also removed some of the officers who supported the previous Kapp putsch.

The center-right parties are committed to the military and deeply suspicious of our efforts to reform it.
