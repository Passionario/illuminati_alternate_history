title: Military Policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: military_policy_timer += 6; month_actions += 1
view-if: spd_in_government = 1 and reichswehr_minister_party = "SPD" and military_policy_timer <= 0 and disband_military = 0
card-image: img/portraits/LeberJulius.jpg

= Military Policy

The Reichswehr is a deeply reactionary institution that has been generally hostile to any notion of socialism or democracy. However, we now control the military as part of the government. What should we do to reform it?

- @increase_funding: Increase funding for the military to purchase its loyalty.
- @decrease_funding: Reduce funding for the military so we have more for social spending.
- @reform: Conduct a thorough reform of the ranks, reducing the influence of reactionaries and making it more hospitable to us.
- @disband: Declare total demilitarization and disband the military.
- @root: No change to military policy.
- @easy_discard

@increase_funding
subtitle: -1 budget
choose-if: budget >= 1
unavailable-subtitle: We do not have the budget.
on-arrival: budget -= 1; coalition_dissent -= 1; reichswehr_loyalty += 0.1; reichswehr_strength += 20; kpd_coalition_dissent += 1; west_relation -= 1; east_relation -= 1; reparations_negotiation -= 1

The military and the center-right parties are pleasantly surprised that we would increase military funding. Our membership tends to be anti-military, and is less enthused. Germany's neighbors are watching our military buildup with worry. [? if reparations >= 0 : This is not good for the reparations negotiations. ?]

@decrease_funding
subtitle: +1 budget
unavailable-subtitle: There is nothing left to cut.
choose-if: reichswehr_strength > 0
on-arrival: budget += 1; coalition_dissent += 1 if in_grand_coalition == 1; reichswehr_militancy -= 1; reichswehr_strength -= 20; reichswehr_loyalty -= 0.05; coup_progress += 1; reichswehr_goal_completed = 1;

The military has fewer guns and fewer soldiers. There is no hint of a coup yet, but we do not know where the line will be drawn. The center-right parties are committed to the military and deeply opposed to our budget cuts.

@disband
view-if: reichswehr_strength = 0 and disband_military = 0
unavailable-subtitle: The judiciary will never allow it.
choose-if: judicial_reform >= 3
go-to: disband_good if (pacifism >= 5 and nationalism <= 45); disband_fail if (pacifism <= 4 or nationalism >= 46);

Now that the military exists mostly on paper, we have decided to put a nail in its coffin and formally disband it.

@disband_good
on-arrival: disband_military = 1; coalition_dissent += 2 if (in_grand_coalition or in_weimar_coalition); reichswehr_militancy = 0; reichswehr_loyalty = 0; left_dissent -= 20; center_dissent += 20; coup_progress += 2; reichswehr_goal_completed = 1; reichswehr_minister = "N/A"; reichswehr_minister_party = "N/A"; kpd_relation += 15; workers_kpd += 5; unemployed_kpd += 5; workers_spd += 5; unemployed_spd += 5; new_middle_spd -= 5; old_middle_spd -= 5; z_relation -= 10; dvp_relation -= 10; communist_coalition += 2; rfb_strength += 100; rfb_militancy += 0.1; sa_strength += 50; sh_strength += 50; west_relation += 3; east_relation += 3; reparations_negotiation += 5

Our attempt is successful and Reichswehr is no more! Our European neighbours praise our decision as the crowning moment of Pax Versailles. The centrists and reactionaries at home are fuming with terror and anger. 

@disband_fail
on_arrival: disband_military = 1; coalition_dissent += 1; reichswehr_loyalty -= 0.05; coup_progress += 1; z_relation -= 3; dvp_relation -= 3; sa_strength += 25; sh_strength += 25; nationalism += 8; pacifism -= 2; 

However, our attempt to disband the military is met with fierce resistance and public protest. We are forced to abandon our approach.

@reform
subtitle: -1 budget
choose-if: budget >= 1
unavailable-subtitle: We do not have the budget.
on-arrival: coalition_dissent += 1 if in_grand_coalition == 1; reichswehr_loyalty += 0.15; reichswehr_militancy -= 1; budget -= 1; coup_progress += 1

Our efforts are at least somewhat successful; we have changed the recruiting and training patterns so that more soldiers from non-reactionary backgrounds can join, hopefully changing the culture of the forces. We have also removed some of the officers who supported the previous Kapp putsch.

The center-right parties are committed to the military and deeply suspicious of our efforts to reform it.
